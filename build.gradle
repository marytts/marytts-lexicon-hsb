plugins {
    id 'de.dfki.mary.lexicon-compiler' version '0.3.0'
    id 'maven-publish'
    id 'signing'
}

group 'de.dfki.mary'
version '0.2.0-SNAPSHOT'
description 'Upper Sorbian lexicon for MaryTTS'

ext {
    url = 'https://github.com/marytts/marytts-lexicon-hsb'
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url = project.url
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0 only'
                        url = 'https://spdx.org/licenses/LGPL-3.0-only.html'
                    }
                }
                developers {
                    developer {
                        id = 'psibre'
                        name = 'Ingmar Steiner'
                    }
                    developer {
                        id = 'aStereoID'
                        name = 'Astrid Schmiedel'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/marytts/marytts-lexicon-hsb.git'
                    developerConnection = 'scm:git:ssh://git@github.com:marytts/marytts-lexicon-hsb.git'
                    url = project.url
                }
            }
        }
    }
    repositories {
        maven {
            name 'OSSRH'
            url isReleaseVersion ? 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                    : 'https://oss.sonatype.org/content/repositories/snapshots'
            credentials(PasswordCredentials)
        }
    }
}

signing {
    required { isReleaseVersion }
    sign publishing.publications.mavenJava
}

tasks.withType(Sign).configureEach {
    onlyIf { isReleaseVersion }
}
